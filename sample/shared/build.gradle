import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

plugins {
    id("kotlin-multiplatform")
    id("com.android.library")
    id("kotlin-parcelize")
    id("com.arkivanov.parcelize.darwin")
}

android {
    namespace "com.arkivanov.parcelize.sample"

    compileSdkVersion 33

    defaultConfig {
        targetSdkVersion 33
        minSdkVersion 28
    }
}

kotlin {
    android()
    iosX64()
    iosArm64()
    iosSimulatorArm64()

    targets.forEach {
        if (it instanceof KotlinNativeTarget) {
            it.binaries {
                framework {
                    baseName = "ParcelizeSample"
                    export("com.arkivanov.parcelize.darwin:runtime:0.1.0")
                }
            }
        }
    }

    sourceSets {
        iosMain {
            dependsOn commonMain

            dependencies {
                api "com.arkivanov.parcelize.darwin:runtime:0.1.0"
            }
        }


        iosX64Main {
            dependsOn iosMain
        }
        iosArm64Main {
            dependsOn iosMain
        }
        iosSimulatorArm64Main {
            dependsOn iosMain
        }
    }
}
